plugins {
    id 'java'
    id 'application'
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url = uri("https://maven.pkg.github.com/richardrodgers/packr")
        credentials {
            username = project.findProperty("gpr.user") ?: "richardrodgers"
            password = project.findProperty("gpr.key") ?: System.getenv("GH_TOKEN")
        }
    }
}

dependencies {
    implementation 'info.picocli:picocli:4.7.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.2'
    implementation 'org.hibernate.validator:hibernate-validator:8.0.0.Final'
    //implementation 'org.hibernate.validator:hibernate-validator-cdi:8.0.0.Final'
    implementation 'org.glassfish:jakarta.el:4.0.2'
    implementation 'org.modrepo:packr:1.0'
}

group 'org.modrepo'
version '1.0.0-SNAPSHOT'

ext {
    developerName1 = "Richard Rodgers"
    developerId1 = "http://orcid.org/0000-0003-1412-5595"
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

mainClassName = 'org.modrepo.bagmatic.Bagmatic'

jar {
    manifest {
        attributes('Main-Class': "${mainClassName}")
    }
}

task uberJar(type: Jar) {
    duplicatesStrategy = 'include'
    manifest {
        attributes 'Main-Class': "${mainClassName}"
    }
    archiveBaseName = "${rootProject.name}"
    from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}
